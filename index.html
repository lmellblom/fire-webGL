
<!-- 	Done in the project TNM084 at LinkÃ¶ping University 2015.
		by: LinnÃ©a Mellblom. 
		Some great info about noise: http://webstaff.itn.liu.se/~stegu/TNM084-2014/simplexnoise.pdf
-->
<!DOCTYPE html>
<html>

<head>
  <title>I see fire ðŸ”¥</title>
  <meta http-equiv="content-type" content="text/html; charset=utf-8">

  <!-- FONTS-->
  <link href='http://fonts.googleapis.com/css?family=Give+You+Glory' rel='stylesheet' type='text/css'>
  <link href='http://fonts.googleapis.com/css?family=Lato:300' rel='stylesheet' type='text/css'>
  
  <link rel="stylesheet" href="style.css">

  <script type="text/javascript" src="glMatrix.js"></script> 

  <!-- The shaders -->
  <script id="shader-fs" type="x-shader/x-fragment">
    #ifdef GL_ES
    precision highp float;
    #endif

    // inputs
  	varying vec2 vTextureCoord;
  	uniform float uTime; 
  	uniform float selectedNoise;
  	uniform bool addEffect; 
  	uniform float effectWidth;
  	uniform float effectHeight;
  	uniform bool sameSize;

	#define fireRGB vec3(1.5,1.0,0.5) // used to desice the color of the fire

  	// --------------- FLOW NOISE --------------------------------
	//
	// GLSL implementation of 2D "flow noise" as presented
	// by Ken Perlin and Fabrice Neyret at Siggraph 2001.
	// (2D simplex noise with analytic derivatives and
	// in-plane rotation of generating gradients,
	// in a fractal sum where higher frequencies are
	// displaced (advected) by lower frequencies in the
	// direction of their gradient. For details, please
	// refer to the 2001 paper "Flow Noise" by Perlin and Neyret.)
	//
	// Author: Stefan Gustavson (stefan.gustavson@liu.se)
	// Distributed under the terms of the MIT license.  	
  	
	// Helper constants
	#define F2 0.366025403
	#define G2 0.211324865
	#define K 0.0243902439 // 1/41

	// Permutation polynomial
	float permute(float x) {
	  return mod((34.0 * x + 1.0)*x, 289.0);
	}

	// Gradient mapping with an extra rotation.
	vec2 grad2(vec2 p, float rot) {
	#if 1
	// Map from a line to a diamond such that a shift maps to a rotation.
	  float u = permute(permute(p.x) + p.y) * K + rot; // Rotate by shift
	  u = 4.0 * fract(u) - 2.0;
	  return vec2(abs(u)-1.0, abs(abs(u+1.0)-2.0)-1.0);
	#else
	#define TWOPI 6.28318530718
	// For more isotropic gradients, sin/cos can be used instead.
	  float u = permute(permute(p.x) + p.y) * K + rot; // Rotate by shift
	  u = fract(u) * TWOPI;
	  return vec2(cos(u), sin(u));
	#endif
	}

	float srdnoise(in vec2 P, in float rot, out vec2 grad) {

	  // Transform input point to the skewed simplex grid
	  vec2 Ps = P + dot(P, vec2(F2));

	  // Round down to simplex origin
	  vec2 Pi = floor(Ps);

	  // Transform simplex origin back to (x,y) system
	  vec2 P0 = Pi - dot(Pi, vec2(G2));

	  // Find (x,y) offsets from simplex origin to first corner
	  vec2 v0 = P - P0;

	  // Pick (+x, +y) or (+y, +x) increment sequence
	  vec2 i1 = (v0.x > v0.y) ? vec2(1.0, 0.0) : vec2 (0.0, 1.0);

	  // Determine the offsets for the other two corners
	  vec2 v1 = v0 - i1 + G2;
	  vec2 v2 = v0 - 1.0 + 2.0 * G2;

	  // Wrap coordinates at 289 to avoid float precision problems
	  Pi = mod(Pi, 289.0);

	  // Calculate the circularly symmetric part of each noise wiggle
	  vec3 t = max(0.5 - vec3(dot(v0,v0), dot(v1,v1), dot(v2,v2)), 0.0);
	  vec3 t2 = t*t;
	  vec3 t4 = t2*t2;

	  // Calculate the gradients for the three corners
	  vec2 g0 = grad2(Pi, rot);
	  vec2 g1 = grad2(Pi + i1, rot);
	  vec2 g2 = grad2(Pi + 1.0, rot);

	  // Compute noise contributions from each corner
	  vec3 gv = vec3(dot(g0,v0), dot(g1,v1), dot(g2,v2)); // ramp: g dot v
	  vec3 n = t4 * gv;  // Circular kernel times linear ramp

	  // Compute partial derivatives in x and y
	  vec3 temp = t2 * t * gv;
	  vec3 gradx = temp * vec3(v0.x, v1.x, v2.x);
	  vec3 grady = temp * vec3(v0.y, v1.y, v2.y);
	  grad.x = -8.0 * (gradx.x + gradx.y + gradx.z);
	  grad.y = -8.0 * (grady.x + grady.y + grady.z);
	  grad.x += dot(t4, vec3(g0.x, g1.x, g2.x));
	  grad.y += dot(t4, vec3(g0.y, g1.y, g2.y));
	  grad *= 40.0;

	  // Add contributions from the three corners and return
	  return 40.0 * (n.x + n.y + n.z);
	}

	// -------------------- END FLOW NOISE -----------------------------

	// ------------------ SIMPLEX NOISE --------------------------------
	// Description : Array and textureless GLSL 2D/3D/4D simplex
	// noise functions.
	// Author : Ian McEwan, Ashima Arts.
	// Maintainer : ijm
	// Lastmod : 20110822 (ijm)
	// License : Copyright (C) 2011 Ashima Arts. All rights reserved.
	// Distributed under the MIT License.
	// https://github.com/ashima/webgl-noise
	//

	vec3 mod289(vec3 x) {
	  return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 mod289(vec4 x) {
	  return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 permute(vec4 x) {
	     return mod289(((x*34.0)+1.0)*x);
	}

	vec4 taylorInvSqrt(vec4 r)
	{
	  return 1.79284291400159 - 0.85373472095314 * r;
	}

	float snoise(vec3 v)
	{
	  const vec2 C = vec2(1.0/6.0, 1.0/3.0) ;
	  const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

	  // First corner
	  vec3 i = floor(v + dot(v, C.yyy) );
	  vec3 x0 = v - i + dot(i, C.xxx) ;

	  // Other corners
	  vec3 g = step(x0.yzx, x0.xyz);
	  vec3 l = 1.0 - g;
	  vec3 i1 = min( g.xyz, l.zxy );
	  vec3 i2 = max( g.xyz, l.zxy );

	  // x0 = x0 - 0.0 + 0.0 * C.xxx;
	  // x1 = x0 - i1 + 1.0 * C.xxx;
	  // x2 = x0 - i2 + 2.0 * C.xxx;
	  // x3 = x0 - 1.0 + 3.0 * C.xxx;
	  vec3 x1 = x0 - i1 + C.xxx;
	  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
	  vec3 x3 = x0 - D.yyy; // -1.0+3.0*C.x = -0.5 = -D.y

	  // Permutations
	  i = mod289(i);
	  vec4 p = permute( permute( permute(
	             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
	           + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
	           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

	  // Gradients: 7x7 points over a square, mapped onto an octahedron.
	  // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
	  float n_ = (1.0/7.0);
	  vec3 ns = n_ * D.wyz - D.xzx;

	  vec4 j = p - 49.0 * floor(p * ns.z * ns.z); // mod(p,7*7)

	  vec4 x_ = floor(j * ns.z);
	  vec4 y_ = floor(j - 7.0 * x_ ); // mod(j,7)

	  vec4 x = x_ *ns.x + ns.yyyy;
	  vec4 y = y_ *ns.x + ns.yyyy;
	  vec4 h = 1.0 - abs(x) - abs(y);

	  vec4 b0 = vec4( x.xy, y.xy );
	  vec4 b1 = vec4( x.zw, y.zw );

	  vec4 s0 = floor(b0)*2.0 + 1.0;
	  vec4 s1 = floor(b1)*2.0 + 1.0;
	  vec4 sh = -step(h, vec4(0.0));

	  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy;
	  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww;

	  vec3 p0 = vec3(a0.xy,h.x);
	  vec3 p1 = vec3(a0.zw,h.y);
	  vec3 p2 = vec3(a1.xy,h.z);
	  vec3 p3 = vec3(a1.zw,h.w);

	  // Normalise gradients
	  vec4 norm = taylorInvSqrt( vec4(dot(p0,p0), dot(p1,p1), dot(p2,p2), dot(p3,p3)) );
	  p0 *= norm.x;
	  p1 *= norm.y;
	  p2 *= norm.z;
	  p3 *= norm.w;

	  // Mix final noise value
	  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
	  m = m * m;
	  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
	                                dot(p2,x2), dot(p3,x3) ) );
	}
	// -------------------- END SIMPLEX NOISE -----------------------------

	// Different kind of combination of noise to display different kind of fire

	float fireFlowNoise(vec2 p, float t){
		vec2 g1, g2; // gradient out for the flow noise

		// adding more on the x to make the flame higher than the width, adding time to y so that the flames will go uppwards, second variable will change rotation
		float n = 0.32*abs(srdnoise(vec2(8.0*p.x, 2.0*p.y+0.4*t), 0.1*t, g1))+0.1; 
		n += 0.5*abs(srdnoise(vec2(16.0*p.x, 4.0*p.y+0.3*t), 0.2*g1.x, g1));
  		n += 0.8 * abs(srdnoise(vec2(8.0*p.x+0.1*t, p.y+0.2*t), 0.15*t, g1))+0.3;
  		n = 1.0-abs(n); // a bit more sharp

  		return n;
	}

	float fireSimplexNoise(vec2 p, float t){
		// adding more on the x to make the flame higher than the width, adding time to y so that the flames will go uppwards
		float n = abs(snoise(vec3(4.0*p.x,p.y+0.3*t,0.2*t)))*0.5+0.3;
  		n += 0.5 * (snoise(vec3(16.0*p.x,4.0*p.y+0.6*t,0.3*t)));
  		n += 0.25 * (snoise(vec3(32.0*p.x+0.4*t, 8.0*p.y+0.7*t,0.4*t)));

		return 1.0-abs(n); // a bit more sharp
	}

	float fireSimplexNoise2(vec2 p, float t){
		// adding more on the x to make the flame higher than the width, adding time to y so that the flames will go uppwards
		float n = abs(snoise(vec3(4.0*p.x,p.y+0.2*t,0.2*t)))*0.5-0.3;
  		n += 0.5 * abs(snoise(vec3(16.0*p.x,4.0*p.y+0.6*t,0.2*t)));
  		n += 0.25 * abs(snoise(vec3(32.0*p.x, 8.0*p.y+0.1*t,0.4*t)));

  		float n1 = 0.5*snoise(vec3(32.0*p.x, 8.0*p.y+0.1*t,0.3*t))+0.1;
  		n1 = 1.0 - abs(n1); // a bit more sharp
  		n1 += 0.5 * snoise(vec3(16.0*p.x+0.1*t, 8.0*p.y+0.6*t, 0.5*t));

  		return mix(n1,n,0.5);
	}

	// adding a effect that cut out cirlces in the fire. 
	float circleEffect(vec2 p){
		// to make a check pattern based on the input of the user how many in row anf height
		vec2 tileSize = vec2(1.0/effectWidth, 1.0/effectHeight);  
		vec2 p2 = vec2(floor(p / tileSize));
		// the individual coordinated of each tile. 
		vec2 tile_coord = abs(mod(p/tileSize, 1.0)-0.5);
		
		float effect = (tile_coord.x*tile_coord.x+tile_coord.y*tile_coord.y)*6.0; // make a circle

		return effect;
	}

	// will make new coordinates for each tile of the check pattern
	vec2 newCoordinates(vec2 p){
		vec2 tileSize = vec2(1.0/effectWidth, 1.0/effectHeight);
		vec2 p2 = vec2(floor(p / tileSize)); 
		vec2 tileCoord = abs(mod(p/tileSize, 1.0));

		return tileCoord;
	}

    void main(void) {
        vec2 p = vTextureCoord * 1.0; // point in space
        vec2 p2 = newCoordinates(p); // the coordinates in each tile / check pattern. 
        float t = uTime;
        float alpha = 1.0;

  		// which fire noise to use depending on the user. Flow if 1.0, Simplex if 0.0 and Simplex2 if 0.5. 
  		float n = ( selectedNoise == 1.0 ?  fireFlowNoise(p,t) : (selectedNoise==0.0 ? fireSimplexNoise(p,t) : fireSimplexNoise2(p,t)) ) ;

  		// the higher in y, the more will the flame go away. 
  		float distortionScale = 4.5;
  		float distortionBias = 0.1;
  		float perturb = ( (1.0-p.y) * distortionScale) + distortionBias;
  		float perturb2 =( (1.0-p2.y) * 2.8) + distortionBias; // based on the coordinates of each tile in the check pattern. 

  		// only add pertub if the addEffect is checked. If same size is checked, then every tile will have equally sized "circle" of fire
  		n = addEffect && !sameSize || !addEffect  ? n * perturb : n*perturb2 ; 

  		// cut ut the effect of a cirle if the box on the website is checked. 
 		n = (addEffect ? n+circleEffect(p) : n); 

  		// the firecolor
  		vec3 fireColor = vec3(fireRGB.r-n, fireRGB.g-n, fireRGB.b-n);

  		gl_FragColor = vec4(fireColor, alpha);
    }
  </script>

   <script id="shader-vs" type="x-shader/x-vertex">
   	// inputs
    attribute vec3 aVertexPosition;
    attribute vec2 aTextureCoord;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;

    uniform vec2 u_resolution; 

    // output to the fragment shader
    varying vec2 vTextureCoord;

    void main(void) {
    	vec2 aPos = vec2(aVertexPosition.x,aVertexPosition.y);

    	// to be able to use pixelvalues instead of clipspace..
    	// -> http://www.html5rocks.com/en/tutorials/webgl/webgl_fundamentals/
    	vec2 zeroToOne = aPos / u_resolution;
    	vec2 zeroToTwo = zeroToOne * 2.0;
    	vec2 clipSpace = zeroToTwo - 1.0;

    	vec3 pos = vec3(clipSpace * vec2(1,-1), aVertexPosition.z);

        gl_Position = uPMatrix * uMVMatrix * vec4(pos, 1.0);
        vTextureCoord = aTextureCoord; 
    }
   </script>
   <!-- WebGL stuff. InitWebGL, initShaders, initBuffers etc -->
   <script type="text/javascript" src="myscripts.js"></script>

</head>

<body onload="webGLStart();">
	<div id="theCanvas">
		<canvas id="glcanvas" width="400" height="400">
    Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
		</canvas>
	</div>

	<div id="choices">
  		<h1>Procedural Fire</h1>

  		<form>
  			<p class="boldHeader">Noise: </p>
  			<input type="radio" name="noise" id="simplex1" value="simplex1">Simplex 1  
  			<input type="radio" name="noise" id="simplex2" value="simplex2">Simplex 2
  			<input type="radio" name="noise" id="flow" value="flow" checked>Flow
  			<br><br>

  			<p class="boldHeader">Effects: </p>
  			<input type="checkbox" name="addEffect" id="addEffect" value="addEffect">Cut out circle in the fire <br>
  			&nbsp&nbsp&nbsp&nbsp&nbsp&nbsp&nbsp Number in width: 
  			<select id="numberWidth">
				<option value="1.0" selected>1</option>
				<option value="2.0">2</option>
				<option value="3.0">3</option>
				<option value="4.0">4</option>
			</select>
			<br>
			&nbsp&nbsp&nbsp&nbsp&nbsp&nbsp&nbsp Number in height: 
			<select id="numberHeight">
				<option value="1.0" selected>1</option>
				<option value="2.0">2</option>
				<option value="3.0">3</option>
				<option value="4.0">4</option>
			</select>
			<br>
			&nbsp&nbsp&nbsp&nbsp&nbsp<input type="checkbox" name="sameSize" id="sameSize" value="sameSize">Same size of fire in circle<br>

		</form> 
	</div>
</body>

<!-- GOOGLE analytic-->
<script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-58530564-1', 'auto');
  ga('send', 'pageview');

</script>
</html> 