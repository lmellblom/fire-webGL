
<!DOCTYPE html>
<html>

<head>
  <title>游댠 Procedural fire with webGL</title>
  <meta http-equiv="content-type" content="text/html; charset=utf-8">

  <!-- FONTS-->
  <link href='http://fonts.googleapis.com/css?family=Josefin+Sans' rel='stylesheet' type='text/css'>
  <link rel="stylesheet" href="style.css">

  <script type="text/javascript" src="glMatrix.js"></script> 

  <!-- The shaders -->
  <script id="shader-fs" type="x-shader/x-fragment">
    #ifdef GL_ES
    precision highp float;
    #endif

    // input from vertex shader
  	varying vec2 vTextureCoord;
  	uniform float uTime; 
  	uniform float selectedNoise;

  	// our texture
	uniform sampler2D u_image;


	#define fireRGB vec3(1.5,1.0,0.5)

  	// --------------- FLOW NOISE --------------------------------
	// Helper constants
	#define F2 0.366025403
	#define G2 0.211324865
	#define K 0.0243902439 // 1/41

	// Permutation polynomial
	float permute(float x) {
	  return mod((34.0 * x + 1.0)*x, 289.0);
	}

	// Gradient mapping with an extra rotation.
	vec2 grad2(vec2 p, float rot) {
	#if 1
	// Map from a line to a diamond such that a shift maps to a rotation.
	  float u = permute(permute(p.x) + p.y) * K + rot; // Rotate by shift
	  u = 4.0 * fract(u) - 2.0;
	  return vec2(abs(u)-1.0, abs(abs(u+1.0)-2.0)-1.0);
	#else
	#define TWOPI 6.28318530718
	// For more isotropic gradients, sin/cos can be used instead.
	  float u = permute(permute(p.x) + p.y) * K + rot; // Rotate by shift
	  u = fract(u) * TWOPI;
	  return vec2(cos(u), sin(u));
	#endif
	}

	float srdnoise(in vec2 P, in float rot, out vec2 grad) {

	  // Transform input point to the skewed simplex grid
	  vec2 Ps = P + dot(P, vec2(F2));

	  // Round down to simplex origin
	  vec2 Pi = floor(Ps);

	  // Transform simplex origin back to (x,y) system
	  vec2 P0 = Pi - dot(Pi, vec2(G2));

	  // Find (x,y) offsets from simplex origin to first corner
	  vec2 v0 = P - P0;

	  // Pick (+x, +y) or (+y, +x) increment sequence
	  vec2 i1 = (v0.x > v0.y) ? vec2(1.0, 0.0) : vec2 (0.0, 1.0);

	  // Determine the offsets for the other two corners
	  vec2 v1 = v0 - i1 + G2;
	  vec2 v2 = v0 - 1.0 + 2.0 * G2;

	  // Wrap coordinates at 289 to avoid float precision problems
	  Pi = mod(Pi, 289.0);

	  // Calculate the circularly symmetric part of each noise wiggle
	  vec3 t = max(0.5 - vec3(dot(v0,v0), dot(v1,v1), dot(v2,v2)), 0.0);
	  vec3 t2 = t*t;
	  vec3 t4 = t2*t2;

	  // Calculate the gradients for the three corners
	  vec2 g0 = grad2(Pi, rot);
	  vec2 g1 = grad2(Pi + i1, rot);
	  vec2 g2 = grad2(Pi + 1.0, rot);

	  // Compute noise contributions from each corner
	  vec3 gv = vec3(dot(g0,v0), dot(g1,v1), dot(g2,v2)); // ramp: g dot v
	  vec3 n = t4 * gv;  // Circular kernel times linear ramp

	  // Compute partial derivatives in x and y
	  vec3 temp = t2 * t * gv;
	  vec3 gradx = temp * vec3(v0.x, v1.x, v2.x);
	  vec3 grady = temp * vec3(v0.y, v1.y, v2.y);
	  grad.x = -8.0 * (gradx.x + gradx.y + gradx.z);
	  grad.y = -8.0 * (grady.x + grady.y + grady.z);
	  grad.x += dot(t4, vec3(g0.x, g1.x, g2.x));
	  grad.y += dot(t4, vec3(g0.y, g1.y, g2.y));
	  grad *= 40.0;

	  // Add contributions from the three corners and return
	  return 40.0 * (n.x + n.y + n.z);
	}

	// -------------------- END FLOW NOISE -----------------------------

	// ------------------ SIMPLEX NOISE --------------------------------
	// Description : Array and textureless GLSL 2D/3D/4D simplex
	// noise functions.
	// Author : Ian McEwan, Ashima Arts.
	// Maintainer : ijm
	// Lastmod : 20110822 (ijm)
	// License : Copyright (C) 2011 Ashima Arts. All rights reserved.
	// Distributed under the MIT License.
	// https://github.com/ashima/webgl-noise
	//

	vec3 mod289(vec3 x) {
	  return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 mod289(vec4 x) {
	  return x - floor(x * (1.0 / 289.0)) * 289.0;
	}

	vec4 permute(vec4 x) {
	     return mod289(((x*34.0)+1.0)*x);
	}

	vec4 taylorInvSqrt(vec4 r)
	{
	  return 1.79284291400159 - 0.85373472095314 * r;
	}

	float snoise(vec3 v)
	{
	  const vec2 C = vec2(1.0/6.0, 1.0/3.0) ;
	  const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

	// First corner
	  vec3 i = floor(v + dot(v, C.yyy) );
	  vec3 x0 = v - i + dot(i, C.xxx) ;

	// Other corners
	  vec3 g = step(x0.yzx, x0.xyz);
	  vec3 l = 1.0 - g;
	  vec3 i1 = min( g.xyz, l.zxy );
	  vec3 i2 = max( g.xyz, l.zxy );

	  // x0 = x0 - 0.0 + 0.0 * C.xxx;
	  // x1 = x0 - i1 + 1.0 * C.xxx;
	  // x2 = x0 - i2 + 2.0 * C.xxx;
	  // x3 = x0 - 1.0 + 3.0 * C.xxx;
	  vec3 x1 = x0 - i1 + C.xxx;
	  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
	  vec3 x3 = x0 - D.yyy; // -1.0+3.0*C.x = -0.5 = -D.y

	// Permutations
	  i = mod289(i);
	  vec4 p = permute( permute( permute(
	             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
	           + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
	           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

	// Gradients: 7x7 points over a square, mapped onto an octahedron.
	// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
	  float n_ = (1.0/7.0);
	  vec3 ns = n_ * D.wyz - D.xzx;

	  vec4 j = p - 49.0 * floor(p * ns.z * ns.z); // mod(p,7*7)

	  vec4 x_ = floor(j * ns.z);
	  vec4 y_ = floor(j - 7.0 * x_ ); // mod(j,7)

	  vec4 x = x_ *ns.x + ns.yyyy;
	  vec4 y = y_ *ns.x + ns.yyyy;
	  vec4 h = 1.0 - abs(x) - abs(y);

	  vec4 b0 = vec4( x.xy, y.xy );
	  vec4 b1 = vec4( x.zw, y.zw );

	  vec4 s0 = floor(b0)*2.0 + 1.0;
	  vec4 s1 = floor(b1)*2.0 + 1.0;
	  vec4 sh = -step(h, vec4(0.0));

	  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy;
	  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww;

	  vec3 p0 = vec3(a0.xy,h.x);
	  vec3 p1 = vec3(a0.zw,h.y);
	  vec3 p2 = vec3(a1.xy,h.z);
	  vec3 p3 = vec3(a1.zw,h.w);

	// Normalise gradients
	  vec4 norm = taylorInvSqrt( vec4(dot(p0,p0), dot(p1,p1), dot(p2,p2), dot(p3,p3)) );
	  p0 *= norm.x;
	  p1 *= norm.y;
	  p2 *= norm.z;
	  p3 *= norm.w;

	// Mix final noise value
	  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
	  m = m * m;
	  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
	                                dot(p2,x2), dot(p3,x3) ) );
	}
	// -------------------- END SIMPLEX NOISE -----------------------------

	// own functions to determine which noise to the fire to use, p is the position and t is the time.  

	// f칬r att f친 l친gor, m친ste l친ga vara "l칛ngre" i y-led. d칛rf칬r h칬gre x 칛n y. 

	float fireFlowNoise(vec2 p, float t){
		vec2 g1, g2; // gradient out for the flow noise

		// ju h칬gre upp noise 칛r, desto mer ska det flyttas i sidled.. allts친 plussa p친 x.  beroende p친 y.
		float sideWind =0.2* t  ;//0.4+p.y*t;
		float sidew = 0.2*t;// + p.y*p.y;

		float n = 0.32*abs(srdnoise(vec2(8.0*p.x, 2.0*p.y+0.4*t), 0.1*t, g1)); // -0.3 f칬r l칛ngre l친gor, +ett tal * tiden f칬r att animera upp친t, tal * t 칛r f칬r rotaionen
		n += 0.5*abs(srdnoise(vec2(32.0*p.x, 4.0*p.y+0.3*t), 0.2*g1.x, g1));
  		n += 0.8 * abs(srdnoise(vec2(16.0*p.x+sideWind, p.y), sidew, g1))+0.3;

  		//n += abs(srdnoise(vec2(2.0*p.x, p.y+0.2*t), 0.2+g1.x*0.1, g2));
  		//n += (srdnoise(vec2(p.x, p.y), 0.1*t, g1));


  		n = 1.0-abs(n); // g칬ra lite skarpare

  		return n;
	}
	float fireSimplexNoise(vec2 p, float t){
		float n = (snoise(vec3(8.0*p.x,p.y+0.2*t,0.2*t)))*0.5+0.2;
  		n += 0.5 * (snoise(vec3(32.0*p.x,4.0*p.y+0.2*t,0.2*t)));
  		n += 0.25 * (snoise(vec3(64.0*p.x, 8.0*p.y+0.1*t,0.4*t)));

		return 1.0-abs(n);
	}

	float fireSimplexNoise2(vec2 p, float t){
		// n칛r man plussar p친 y-led in i noisefunktionen, s친 anger man hur mkt den ska animera i h칬jdled. f칬r att g칬ra mer l친gaktiga, m친ste 
		// x och y vara "utt칛njda elr vad man s칛ger". allts친 mer i "rutor" i x-led och d칛rmer blir smalare typ. 
		// om tar abs mellan varje s친 blir det lite skarpare. 

		float n = abs(snoise(vec3(8.0*p.x,p.y+0.2*t,0.2*t)))*0.5+0.2;
  		n += 0.5 * abs(snoise(vec3(32.0*p.x,4.0*p.y+0.2*t,0.2*t)));
  		n += 0.25 * abs(snoise(vec3(64.0*p.x, 8.0*p.y+0.1*t,0.4*t)));
  	
  		return n;//1.0-abs(n);
	}

    void main(void) {
    	/* TODO f칬r att g칬ra elden mer verklighetstrogen
    	- Sk칛rpan i detaljerna
    	- Animeringen i h칬jdled
    	- Hur mycket elden fladdrar i sidled
    	- Hur fort l친gorna byter utseende
    	*/

        vec2 p = vTextureCoord * 1.0; // point in space

        float t = uTime;

        // alpha value
        float alpha = 1.0;

        // beroende p친 vilket y-v칛rde, h칬gre t ju h칬gre upp??
        //t = t + (1.0-p.y)*2.0;

  		// which fire noise to use, om selected noise 칛r 1.0, flow. 0.0 first simplex, else simplex 2. 
  		float n = ( selectedNoise == 1.0 ?  fireFlowNoise(p,t) : (selectedNoise==0.0 ? fireSimplexNoise(p,t) : fireSimplexNoise2(p,t)) ) ;

  		// ju h칬gre upp, desto mindre l친gor, tona bort d친. 
  		float distortionScale = 3.0;
  		float distortionBias = 0.4;
  		float perturb = ( (1.0-p.y) * distortionScale) + distortionBias;
  		n = n * perturb + (1.0-p.y);

  		// g칬ra l친gorna skarpare

  		// different styles of firecolors.. 
  		vec3 fireColor = vec3(fireRGB.r-n, fireRGB.g-n, fireRGB.b-n);

  		gl_FragColor = vec4(fireColor, alpha);
		// ---------------------------------------------------------------
    }
  </script>

   <script id="shader-vs" type="x-shader/x-vertex">
   	// inputs
    attribute vec3 aVertexPosition;
    attribute vec2 aTextureCoord;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;

    uniform vec2 u_resolution; 

    // output to the fragment shader
    varying vec2 vTextureCoord;

    void main(void) {
    	vec2 aPos = vec2(aVertexPosition.x,aVertexPosition.y);

    	// to be able to use pixelvalues instead of clipspace..
    	// -> http://www.html5rocks.com/en/tutorials/webgl/webgl_fundamentals/
    	vec2 zeroToOne = aPos / u_resolution;
    	vec2 zeroToTwo = zeroToOne * 2.0;
    	vec2 clipSpace = zeroToTwo - 1.0;

    	vec3 pos = vec3(clipSpace * vec2(1,-1), aVertexPosition.z);
    	//pos = aVertexPosition;

        gl_Position = uPMatrix * uMVMatrix * vec4(pos, 1.0);
        vTextureCoord = aTextureCoord; 
    }
   </script>
   <!-- - - - - - - - - - - - - - - - - -->

	<script type="text/javascript" src="myscripts.js"></script>

</head>

<body onload="webGLStart();">
  <canvas id="glcanvas" width="500" height="500">
    Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
  </canvas>
  <br>
  <form>
  	<input type="radio" name="noise" id="simplex" value="simplex">Simplex  
  	<input type="radio" name="noise" id="simplex_abs" value="simplex_abs">Simplex with abs
  	<input type="radio" name="noise" id="flow" value="flow" checked>Flow
  </form> 
</body>

</html> 






